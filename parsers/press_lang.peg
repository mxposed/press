grammar press

document            <- statements

statements          <- _* (expr exprs:(expr_end _* expr)*)? _*

expr_end            <- _* ";" / "\n"

expr                <- value / assignment_expr / function_expr / call_expr

assignment_expr     <- name _* "=" _* expr

function_expr       <- "function" _* "(" function_args? ")" _* "{" statements "}"

function_args       <- _* name _* ("," _* name _*)*

call_expr           <- name arg:("(" args:call_args? ")" / " "+ expr)?

call_args           <- _* expr _* exprs:("," _* expr _*)*

value               <- string / number / boolean / null

string              <- '"' ("\\" . / [^"])* '"'  %make_string / "'" ("\\" . / [^'])* "'"  %make_string

number              <- ("0" / [1-9] [0-9]*) ("." [0-9]+)? (("e" / "E") ("+" / "-" / "") [0-9]+)?  %make_number

boolean             <- "true"  %make_boolean / "false"  %make_boolean

null                <- "null"  %make_none

name                <- [a-zA-Z_] [a-zA-Z0-9_]*  %make_name

_                   <-  [\s] / comment

comment             <- line_comment / multiline_comment

line_comment        <- "//" [^\n]*

multiline_comment   <- "/*" (!"*/" .)* "*/"
