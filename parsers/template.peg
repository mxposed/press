grammar template

document            <- template

template            <- text_ (insertion text_)*

inner_template      <- inner_text (insertion inner_text)*

text_               <- (!"[" .)*

inner_text          <- (!("[" / "]") .)*

insertion           <- "[" statements "]"

statements          <- _* (expr (expr_end _* expr)*)? _*

expr_end            <- _* ";" / "\n"

expr                <- assignment_expr / function_expr / call_expr / value

assignment_expr     <- name _* "=" _* expr

function_expr       <- "function" _* "(" function_args? ")" _* "{" statements "}"

function_args       <- _* name _* ("," _* name _*)*

call_expr           <- name ("(" call_args? ")" / " "+ expr / bracket_args)?

call_args           <- _* expr _* ("," _* expr _*)*

bracket_args        <- bracket_arg*

bracket_arg         <- "[" inner_template "]" [\s]*

value               <- string / number / boolean / null

string              <- '"' ("\\" . / [^"])* '"' / "'" ("\\" . / [^'])* "'"

number              <- "-"? ("0" / [1-9] [0-9]*) ("." [0-9]+)? (("e" / "E") ("+" / "-" / "") [0-9]+)?

boolean             <- "true" / "false"

null                <- "null"

name                <- [a-zA-Z_] [a-zA-Z0-9_]*

_                   <-  [\s] / comment

comment             <- line_comment / multiline_comment

line_comment        <- "//" [^\n]*

multiline_comment   <- "/*" (!"*/" .)* "*/"
